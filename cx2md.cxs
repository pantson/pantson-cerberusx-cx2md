Strict

Import brl.filestream
Import brl.process

Function Main:Int()
	Local args:String[] = AppArgs()
	Local input_file:String 
	Local out_file:String
	Local punctuation:String=",.;"
	Local table:Bool
	Local table_header:Bool
	
	Const _method:=0
	Const _class:=1
	
	If args.Length()=1
#If CONFIG="debug"
		input_file="C:\dev\bin\CerberusX\modules\mojo2\cerberusdoc\graphics.cerberusdoc"
		out_file="tmp.md"
#Else
		Error("usage - cx2md.exe input_file out_file")
#End
	Else
		input_file=args[1]
		out_file=args[2]
	End
	
	Local data:String
	
	Local fl:FileStream = FileStream.Open(input_file,"r")
	If fl=Null Then Error("cannot open : "+input_file)
	data =  fl.ReadString()
	fl.Close()
	
	fl = FileStream.Open(out_file,"w")
	If fl=Null Then Error("cannot open : "+out_file)

	Local code_block:Bool = False
	Local class_name:String = ""
	Local anchor:String = ""
	
	Local lines:String[] = data.Split("~n")
	For Local i:Int = 0 Until lines.Length()

		Local line:String = ""
		
		If code_block = True
			' output data with no processing
			If lines[i].Trim().ToLower() = "</pre>"
				line="```"
				code_block=False
			Else
				line = lines[i]
			End
		Else
			' process text
			Local header:Int = 0	
			Local mode:Int
						
			Local line_data:String[] = SplitLine(lines[i]," ~t")

			If line_data.Length()>0
				Select line_data[0].Trim().ToLower()
				Case "<pre>"
					line="```cerberusx"
					code_block = True
				Case ">>"
					line = "##"
				Case ">>>"
					line = "###"
				Case "#"
					line ="#"
					header=1
				Default
					line=line_data[0]
					If line[0..1] = "|"
						If table=False
							table_header=True
						Else
							table_header=False
						End
						table = True
					Else
						table = False					
					End
				End

				For Local j:Int=1 Until line_data.Length()
					' any punctuation?
					Local punctuation_pos:Int = punctuation.Find(line_data[j][(-1)..])
					If punctuation_pos>-1
						' trim last char
						line_data[j] = line_data[j][0..(-1)]
					End
					 
					' could use this to process the first char of words
					' ie detect ! for images
					Select line_data[j][0..1]
					Case "["
						Local parts:String[] = (line_data[j][2..(-2)]).Split("|")
						If parts[0].Find(".")=-1 Then parts[0] = class_name+"."+parts[0]
						If parts.Length()>1
							' got description
							line=line+" ["+parts[0]+"](#"+parts[1]+")"						
						Else
							' copy link for description
							line=line+" ["+parts[0]+"](#"+parts[0]+")"						
						End
					Case "!"
						' nothing needs doing for images
						line=line+" "+line_data[j]
					Case "@"
						' bold
						line=line+" **"+line_data[j][1..]+"**"						
					Default
						line=line+" "+line_data[j]
						If header=2
							If mode=_method And anchor="" Then anchor="<a name=~q"+class_name+"."+line_data[j][0..(line_data[j].Find(":"))]+"~q></a>"
							If mode=_class Then class_name=line_data[j]
						End
													
						If header = 1
							Select line_data[j].ToLower()
							Case "method"
								mode=_method
								header=2
								line = "### Method"
							Case "function"
								mode=_method
								header=2
								line = "### Function"							
							Case "class"
								mode=_class
								header=2
								class_name = ""
								line = "## Class"
							End
						End
					End
					If punctuation_pos>-1
						line+=punctuation[punctuation_pos..(punctuation_pos+1)]
					End
				Next
				header = 0
			End
		End

		If anchor Then fl.WriteString(anchor+"~n")

		If line.Trim()<>""
			If table=True And line[-2..-1]<>"|" Then line+=" |"
		End
		fl.WriteString(line+"~n")
		
		If table_header=True
			Local header:String = ""
			For Local j:Int=1 Until Count(line,"|")
				header+="| :-- "
			Next
			fl.WriteString(header+"|~n")
		End
		anchor=""
	Next
	fl.Close()
	
	Return 0
End

' splits lines using multiple sperators
Function SplitLine:String[](line:String,split:String)
	Local words:String[]
	
	Local these_words:String[] = line.Split(split[0..1])
	
	If split.Length>1
		For Local i:Int = 0 Until these_words.Length()
			Local words1:String[] = SplitLine(these_words[i],split[1..])
			
			For Local j:Int=0 Until words1.Length()
				If words1[j]<>""
					words = words.Resize(words.Length+1)
					words[words.Length-1] = words1[j]
				End
			Next
		Next
	Else
		words = words.Resize(these_words.Length())
		For Local i:Int = 0 Until these_words.Length()
			words[i] = these_words[i]
		Next	
	End
	Return words
End

' finds the number of f in s
Function Count:Int(s:String,f:String)
	Local c:Int=0
	For Local i:Int = 0 Until s.Length()
		If s[i]=f[0] Then c+=1
	Next
	Return c
End